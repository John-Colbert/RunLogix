using Format, Units;

MarginLayout = new ExtentClass(new ThicknessClass(1 inch, 0.5 inches, 1 inch, 1 inch));

class TimeDistanceClass : DataPointClass {
  var int Month;

  Constructor(int month, double distance) {
    super.Constructor(month, distance);
    Month    = month;
  }

  DataPointClass BasePoint() {
    return new DataPointClass(Variable, Value);
  }
}
//=======================================================================

class RunningDocumentsClass: DocumentClass {
  Constructor(guid factid, string name) {
    super.Constructor(factid, name);
  }

  ShowTitle = Paragraph {
    TextHeight: 30 pts;
    ParAlignment: ParAlignments.Right;
    SpaceAfter: 20 pts;
    "Run Logix";
  };
}
//=======================================================================

class SummaryClass: RunningDocumentsClass {
  var ListClass<RawWorkoutClass>       AllRawWorkouts;
  var ListClass<TimeBasedWorkoutClass> AllTimeBasedWorkouts;
  var ListClass<TimeDistanceClass>     MonthlyActualDistance;

  Constructor {
    super.Constructor(#ad080fa6-1765-48f7-862e-dc043ac95a16#, "Summary");
    Workouts.Build;
    MonthlyActualDistance = new(12);
    AllRawWorkouts       = RawWorkouts.Workouts;
    AllTimeBasedWorkouts = Workouts.Workouts;
    GetMonthyActuals();
  }

  override GetDocument = Document(MarginLayout) {
    ShowTitle;
    ShowDistanceChart;
    ShowDiary;
  };

  ShowDistanceChart = VBox(100%) {
    var months = MonthClass.MonthsOfTheYear;

    HAlign: HAligns.Center;
    Background: Colors.WhiteSmoke;
    "{0} Workouts"(Workouts.Workouts.Length);
    Chart(90%, 3 inches) {
      Marker: Markers.Circle(Colors.White, 0.5);
      Type: ChartTypes.Column;
      XAxis: ChartAxis {
        Margin: PadT(4);
        (each months).ShortName;
      };
      ValueAxis: ChartAxis;
      ValueLabel: HBox {
        Transform: TransformClass.Rotate(270 degrees);
        TransformFit: true;
        Measures.DistanceUnitPlural;
      };
      ChartSeries {
        MonthlyActualDistance.ToArray();
      };
    };
  };

  ShowDiary = Block {
    SpaceBefore: 5 pts;

    Paragraph {
      TextHeight: 16 pts;
      "Diary"
    };

    Table (null, null, [1 inches, 0.25 inches, ColumnClass.Expand]) {
      foreach (var rawWorkout in AllRawWorkouts) {
      var timeBasedWorkout = AllTimeBasedWorkouts?.FindFirst(d => d.Filename == rawWorkout.Filename);
      //foreach (var workout in AllTimeBasedWorkouts) {
        string notes       = "";
        string workoutName = "Standard Run";
        datetime workoutDate;
        double actualDistance = 0;
        double actualTime     = 0;


        if (timeBasedWorkout is not null) {
          notes          = timeBasedWorkout.Notes;
          workoutDate    = timeBasedWorkout.WorkoutDate;
          workoutName    = timeBasedWorkout.WorkoutName;
          actualDistance = timeBasedWorkout.TotalActualDistance;
          actualTime     = timeBasedWorkout.TotalActualTime;
        }
        else {
          workoutDate = rawWorkout.StartTime;
          actualDistance = rawWorkout.TotalDistance * Measures.MilesPerMeter;
          actualTime     = rawWorkout.TotalTime;
        }

        if (notes is valid && notes.Length > 0) {
          Row (PadT(5 pts)) {
            Cell { ShowDiaryDate(workoutDate); };
            Cell { Chars.EmDash;           };
            Cell {
              Span {
                TextHeight: 11 pts;
                TextFamily: TextFamilies.TrebuchetMS;
                notes;
              };
            };
          };
          Row {
            Cell;
            Cell;
            Cell {
              Span {
                ShowDetails(workoutName, actualDistance, actualTime);
              };
            };
          };
        }
        else {
          Row (PadT(5 pts)) {
            Cell { ShowDiaryDate(workoutDate); };
            Cell { Chars.EmDash;           };
            Cell {
              Span {
                ShowDetails(workoutName, actualDistance, actualTime);
              };
            };
          };
        }
      }
    };
  };

  ShowDiaryDate(datetime date) = Span {
    TextHeight: 12 pts;
    TextFormat: "MMM d, yyyy";
    TextColor: Colors.DimGray;
    date;
  };

  ShowDistance(double distance) = Span {
    TextDigits: 3;
    distance Space Measures.DistanceUnitPlural;
  };

  ShowTime(double time) = Span {
    TextDigits: 2;
    time Space minutes;
  };

  ShowDetails(string name, double actualDistance, double actualTime) = {
    TextHeight: 9 pts;
    TextColor: Colors.Gray;
    Separator: Space Chars.EmDash Space;
    name;
    ShowDistance(actualDistance);
    ShowTime(actualTime);
  };

  void GetMonthyActuals() {
    for (int i = 0; i < 12; i++)
      MonthlyActualDistance.Add(new TimeDistanceClass(i, 0));

    var chartYear = datetime.Now.Year;
    foreach (var rawWorkout in AllRawWorkouts) {
      var timeBasedWorkout = AllTimeBasedWorkouts?.FindFirst(d => d.Filename == rawWorkout.Filename);

      datetime dt;
      double mDist = 0;

      if (timeBasedWorkout is not null)  {
        dt = timeBasedWorkout.WorkoutDate;
        if (dt.Year == chartYear)
          mDist = timeBasedWorkout.TotalActualDistance;
      }
      else {
        dt = rawWorkout.StartTime;
        if (dt.Year == chartYear)
          mDist = rawWorkout.TotalDistance * Measures.PerMeterFactor;
      }

      if (mDist > 0) {
        var md = MonthlyActualDistance.FindFirst(d => d.Month == dt.Month - 1);
        md.Value += mDist;
        md.Label = Span {
          TextDigits: 1;
          TextBackground: Colors.White;
          md.Value;
        };
      }
    }
  }
}
//======================================================================================

class PerformanceToTargetClass: RunningDocumentsClass {
  var ListClass<TimeBasedWorkoutClass> RunWorkouts;

  Constructor {
    super.Constructor(#029a3f5d-ebfd-4011-8d57-7f87c7b6b324#, "Performance to Target");
    RunWorkouts = Workouts.Workouts;
  }

  override GetDocument = Document(MarginLayout) {
    ShowTitle;

    foreach(var workout in RunWorkouts) {
      Canvas(100%) {
        HBox {
          HAlignment: HAligns.Left;
          TextFormat: "MMM d, yyyy";
          workout.WorkoutDate;
          Space "-" Space;
          workout.WorkoutName;
        };

        HBox {
          HAlignment: HAligns.Right;
          TextFormat: "hh:mmtt";
          workout.StartTime;

          Space "-" Space;

          TextFormat: "hh:mmtt";
          workout.EndTime;
        };
      };

      if (workout.Notes != null && workout.Notes.Length > 0) {
        Paragraph {
          TextHeight: 7 pts;
          workout.Notes;
        };
      }

      Table(null, null) {
        TextHeight: 7 pts;
        Row(PadAll(2 pts)) {
          "Segment";
          "Time(actual)";
          "Target";
          "Pace";
          "Performance";
          "Target "  Measures.DistanceUnitPlural;
          "Actual " + Measures.DistanceUnitPlural;
        };

        foreach (var segment in workout.SegmentList) {
          if (segment.Name == "Open" && segment.ActualTime == 0)
            continue;

          Row(PadAll(2 pts)) {
            segment.Name;

            Span {
              if (segment.Name == "Open")
                TextDigits: 2;

              ShowTime(segment.TargetTime, segment.ActualTime);
            };

            ShowPace(segment.ConvertedTargetPace);
            ShowPace(segment.ActualPace);
            ShowPerformance(segment.Performance);
            ShowDistance(segment.TargetDistance);
            ShowDistance(segment.ActualDistance);
          };
        }

        Row(BorderTB(1)) {
        };

        Row(PadAll(2 pts)) {
          "Summary";
          Span {
            ShowTime(workout.TotalTargetTime, workout.TotalActualTime);
          };

          ShowPace(workout.TotalTargetPace);
          ShowPace(workout.TotalActualPace);
          ShowPerformance(workout.TotalPerformance);
          ShowDistance(workout.TotalTargetDistance);
          ShowDistance(workout.TotalActualDistance);
        };
      };

      Paragraph {};
    }
  };

  ShowTime(double targetTime, double actualTime) = {
    targetTime;
    if (actualTime > 0 && actualTime != targetTime) {
      "(" + { TextDigits: 2; TextColor: new ColorClass(204, 0, 0); } actualTime + ")";
    }
  };

  ShowPace(double pace) = Span {
    var t = new TimeSpanClass(0, 0, 0, pace * 60);
    TextFormat: "00";
    "{0}:{1}"(t.Minute, t.Second);
  };

  ShowDistance(double distance) = Span {
    TextDigits: 3;
    distance;
  };

  ShowPerformance(double performance) = Span {
    var negative = performance < 0;
    var inRange  = performance >= -0.083 && performance <= 0.083;
    var seconds  = performance * 60;

    if (seconds > 59 || seconds < -59) {
      {TextDigits: 2} (seconds / 60);
      "min";
    }
    else {
      {TextDigits: 1} seconds;
      "s";
    }

    if (inRange)
      TextColor: new ColorClass(153, 153, 0);
    else if (negative)
      TextColor: new ColorClass(0, 153, 51);
    else
      TextColor: new ColorClass(204, 0, 0);
  };
}
//====================================================================

class RawRunningDocumentClass: RunningDocumentsClass {

  Constructor {
    super.Constructor(#82bd5cf5-0150-4e79-95c6-3cb36f7824e9#, "All Basic Running Records");
  }

  override GetDocument = Document(MarginLayout) {
    ShowTitle;

    foreach (var workout in RawWorkouts.Workouts) {
      var distance = workout.TotalDistance * Measures.PerMeterFactor;

      Canvas(100%) {
        Table (null, null, [1.25 inches, 0.35 inches, 1.5 inch, ColumnClass.Expand, 1.75 inches]) {
          Row {
            TextHeight: 14 pts;
            SpaceBefore: 10 pts;
            Cell {
              TextFormat: "MMM d, yyyy";
              workout.StartTime;
            };
            Cell(null, 2) {
              ShowDistance(distance);
            };
            Cell {
              var pace = workout.TotalTime / (workout.TotalDistance * Measures.PerMeterFactor);
              ShowPace(pace, Measures.MinutesPerAbbr);
            };
            Cell {
              Span {
                TextFormat: "hh:mmtt";
                workout.StartTime;
                Space "-" Space;
                TextFormat: "hh:mmtt";
                workout.EndTime;
              };
            };
          };

          if (workout.Splits?.Length > 0) {
            Row {
              Cell;
              Cell { "Splits" };
              Cell;
              Cell;
              Cell;
            };
          }

          foreach (var split in workout.Splits) {
            var ele = Utilities.GetElevationMetrics(split.ElevationReadings);
            Row {
              Cell;
              Cell {
                each1;
              };
              Cell;
              Cell {
                ShowPace(split.Time, " min");
              };
              Cell {
                VAlign: VAligns.Bottom;
                ShowElevation(ele);
              };
            };
          }

          if (workout.RemainingDistance?.Distance > 0) {
            var ele = Utilities.GetElevationMetrics(workout.RemainingDistance.ElevationReadings);
            Row {
              Cell;
              Cell {
                "End";
              };
              Cell {
                var dist = workout.RemainingDistance.Distance * Measures.PerMeterFactor;
                ShowDistance(dist);
              };
              Cell {
                ShowPace(workout.RemainingDistance.Time, " min");
              };
              Cell {
                VAlign: VAligns.Bottom;
                ShowElevation(ele);
              };
            };
          }

        };
      };
    }
  };

  ShowPace(double pace, string symbol) = Span {
    var t = new TimeSpanClass(0, 0, 0, pace * 60);
    Span {
      TextFormat: "00";
      "{0}:{1}"(t.Minute, t.Second);
      Span {
        TextHeight: 10 pts;
        symbol;
      }
    };
  };

  ShowDistance(double distance) = Span {
    TextDigits: 3;
    distance;
    Span {
      TextHeight: 10 pts;
      Measures.DistanceAbbr;
    };
  };

  ShowElevation (ElevationClass ele) = Span {
    Span {
      TextDigits: 1;
      Span {
        TextColor: Colors.Green;
        Tex.Uparrow;
      };
      ele.DistanceUp;
    };
    Space;
    Span {
      TextDigits: 1;
      Span {
        TextColor: Colors.Red;
        Tex.Downarrow;
      };
      ele.DistanceDown;
    };
  };

}